pkgname=dwm-git
_pkgname=dwm
pkgver=r1857.4dadaa1
pkgrel=1
pkgdesc="Personal build of the dynamic window manager"
url="http://dwm.suckless.org"
arch=('x86_64' 'aarch64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft-bgra-git' 'yajl')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
source=(dwm.desktop
        "${_pkgname}::git+https://github.com/sainnhe/dwm.git"
        config.h)
md5sums=('7f873efd596b8b30e2cdc09583209874'
         'SKIP'
         'SKIP') # so you can customize config.h

pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  if [[ -f "$srcdir/config.h" ]]; then
    cp -fv "$srcdir/config.h" config.h
  fi
}

build() {
  cd "${srcdir}/${_pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/${_pkgname}"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -m644 -D "${srcdir}/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
